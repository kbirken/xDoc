module workflow.XDocGenerator

import org.eclipse.emf.mwe.utils.*

var targetDir = "src-gen"
var fileEncoding = "ISO-8859-1"
var modelPath = "src/model"
var pdfLatex = "pdflatex"
var release = false
var cleanAfterRun = true
var document

Workflow {

	component = org.eclipse.xtext.mwe.Reader {
		// lookup all resources on the classpath
		 useJavaClassPath = true

		// or define search scope explicitly
//		path = modelPath
		uriFilter = org.eclipse.xtext.mwe.NameBasedFilter {
			extension = "xdoc"
		}
		// this class will be generated by the xtext generator 
		register = org.eclipse.xtext.xdoc.XdocStandaloneSetup {}
		load = {
			slot = "doc"
			type = "Document"
			name = document
			namespaceDelimiter = "disabled namespace delimiter"
		}
	}

	component = org.eclipse.xpand2.Generator {
		expand = "templates::Template::main FOREACH doc"
		fileEncoding = fileEncoding
		outlet = {
			path = targetDir
		}
		globalVar = {
			name = "release"
			value = "${release}"
		}
		globalVar = {
			name = "dir"
			value = targetDir
		}
		globalVar = {
			name = "srcDir"
			value = modelPath
		}
	}
	
	component = workflow.LatexRunner auto-inject {
		outputDir = targetDir
		inputSlot = "doc"
		cleanAfterRun = cleanAfterRun
	}
}
